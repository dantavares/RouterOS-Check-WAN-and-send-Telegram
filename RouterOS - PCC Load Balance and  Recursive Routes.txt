#Global Variables - SET FIRST!!

#WAN Interfaces:
:global wan1 "ether1"
:global wan2 "ether2"

#LAN Interface
:global lan  "ether3"

#Respective WAN Gateways
:global gate1 "10.0.1.1"
:global gate2 "10.0.2.1"

#Public Internet IPs to Check WAN Status
#Used on Recursive Route
:global wck1 "8.8.4.4"
:global wck2 "1.0.0.1"

#############################################################################################################################################################

# Routing Table - Use only for Version > 7.1 

/routing table
add disabled=no fib name=ISP1_table
add disabled=no fib name=ISP2_table

#############################################################################################################################################################

# Firewall Mangle

/ip firewall mangle
add action=mark-connection chain=input connection-state=new in-interface=$wan1 new-connection-mark=ISP1 passthrough=yes
add action=mark-connection chain=input connection-state=new in-interface=$wan2 new-connection-mark=ISP2 passthrough=yes

add action=mark-connection chain=output connection-mark=no-mark connection-state=new new-connection-mark=ISP1 passthrough=yes per-connection-classifier=\
	both-addresses:2/0
add action=mark-connection chain=output connection-mark=no-mark connection-state=new new-connection-mark=ISP2 passthrough=yes per-connection-classifier=\
	both-addresses:2/1

add action=mark-connection chain=prerouting connection-mark=no-mark connection-state=new dst-address-type=!local in-interface=$lan new-connection-mark=\
    ISP1 passthrough=yes per-connection-classifier=both-addresses:2/0
add action=mark-connection chain=prerouting connection-mark=no-mark connection-state=new dst-address-type=!local in-interface=$lan new-connection-mark=\
    ISP2 passthrough=yes per-connection-classifier=both-addresses:2/1

add action=mark-routing chain=prerouting connection-mark=ISP1 in-interface=$lan new-routing-mark=ISP1_table passthrough=yes
add action=mark-routing chain=prerouting connection-mark=ISP2 in-interface=$lan new-routing-mark=ISP2_table passthrough=yes

add action=mark-routing chain=output connection-mark=ISP1 out-interface=$wan1 new-routing-mark=ISP1_table passthrough=yes
add action=mark-routing chain=output connection-mark=ISP2 out-interface=$wan2 new-routing-mark=ISP2_table passthrough=yes

#############################################################################################################################################################

# Firewall NAT

/ip firewall nat
add action=masquerade chain=srcnat out-interface=$wan1
add action=masquerade chain=srcnat out-interface=$wan2

#############################################################################################################################################################
# Routes - Choose Simple Route or Recursive Route, NEVER BOTH!
#############################################################################################################################################################

# Simmple Route

/ip route
add check-gateway=ping disabled=no dst-address=0.0.0.0/0 gateway=$gate1 routing-table=ISP1_table suppress-hw-offload=no
add check-gateway=ping disabled=no dst-address=0.0.0.0/0 gateway=$gate2 routing-table=ISP2_table suppress-hw-offload=no

add distance=1 dst-address=0.0.0.0/0 gateway=$gate1
add distance=2 dst-address=0.0.0.0/0 gateway=$gate2

#############################################################################################################################################################

# Recursive Route - WAN Check

/ip route
add comment="ISP1 Gateway" distance=1 dst-address=$wck1 gateway=$gate1 scope=20
add comment="ISP2 Gateway" distance=1 dst-address=$wck2 gateway=$gate2 scope=20

add check-gateway=ping distance=1 gateway=$wck1 target-scope=20
add check-gateway=ping distance=2 gateway=$wck2 target-scope=20

add check-gateway=ping distance=1 gateway=$wck1 routing-table=ISP1_table target-scope=20
add check-gateway=ping distance=1 gateway=$wck2 routing-table=ISP2_table target-scope=20
